# Urocissa dev tasks
# Install just: cargo install just
# Activate pre-commit hook: git config core.hooksPath .githooks

[private]
help:
    @just --list --unsorted

# ── Backend ────────────────────────────────────────────────────────────────────

# cargo fmt --check
[group('backend')]
backend-format:
    cd gallery-backend && cargo fmt --check

# cargo fmt
[group('backend')]
backend-format-fix:
    cd gallery-backend && cargo fmt

# cargo clippy -- -D warnings
[group('backend')]
backend-check:
    cd gallery-backend && cargo clippy -- -D warnings -A clippy::unwrap_used

# cargo nextest run
[group('backend')]
backend-test:
    cd gallery-backend && cargo nextest run

# cargo deny check
[group('backend')]
backend-deny:
    cd gallery-backend && cargo deny check

# cargo audit
[group('backend')]
backend-audit:
    cd gallery-backend && cargo audit

# cargo build (debug, no embedded frontend) — developer default; matches check/test
[group('backend')]
backend-build:
    cd gallery-backend && cargo build

# cargo build --release --features embed-frontend — production build (CI/deployment)
[group('backend')]
backend-build-release:
    cd gallery-backend && cargo build --release --features embed-frontend

# ── Frontend ───────────────────────────────────────────────────────────────────

# prettier --check
[group('frontend')]
frontend-format:
    cd gallery-frontend && npx prettier --check .

# prettier --write
[group('frontend')]
frontend-format-fix:
    cd gallery-frontend && npx prettier --write .

# vue-tsc + eslint
[group('frontend')]
frontend-check:
    cd gallery-frontend && npx vue-tsc --noEmit && npx eslint .

# vitest run
[group('frontend')]
frontend-test:
    cd gallery-frontend && npm test

# npm run build (npm ci + vue-tsc + vite build)
[group('frontend')]
frontend-build:
    cd gallery-frontend && npm run build

# npm audit
[group('frontend')]
frontend-audit:
    cd gallery-frontend && npm audit

# ── Global ─────────────────────────────────────────────────────────────────────

# Check formatting (backend + frontend)
[group('global')]
format: backend-format frontend-format

# Auto-fix formatting (backend + frontend)
[group('global')]
format-fix: backend-format-fix frontend-format-fix

# Run linters (backend + frontend)
[group('global')]
check: backend-check frontend-check

# Run tests (backend + frontend)
[group('global')]
test: backend-test frontend-test

# Install cargo dev tools (cargo-nextest, cargo-deny, cargo-audit)
[group('global')]
install-dev:
    cargo install cargo-nextest cargo-deny cargo-audit sccache

# Build frontend then backend (debug, no embedded frontend) — developer default
[group('global')]
build: frontend-build backend-build

# Build frontend then backend with embedded assets (release) — production build (CI/deployment)
[group('global')]
build-release: frontend-build backend-build-release

# Remove the dev sandbox's generated app state (sandbox/data); leaves sandbox/images alone
[group('global')]
clean:
    rm -rf sandbox/data

# Build (debug, no embedded frontend) and launch a clean instance against sandbox/{data,images}
[group('global')]
run: clean build
    #!/usr/bin/env sh
    set -e
    mkdir -p sandbox/images
    cd gallery-backend && \
        UROCISSA_CONFIG_HOME="{{justfile_directory()}}/sandbox/data" \
        UROCISSA_DATA_HOME="{{justfile_directory()}}/sandbox/data" \
        UROCISSA_IMAGE_HOME="{{justfile_directory()}}/sandbox/images" \
        cargo run

# Run security audits (backend + frontend)
[group('global')]
audit: backend-audit backend-deny frontend-audit

# Pre-commit check: run format + linter/static checks.
# On main, we enforce full tests as well. This is to support
# test-driven development in development branches.
# Developers know best which tests to fix and what to delegate to CI,
# but then again, these are safe defaults when people are in a hurry.
[group('global')]
precommit:
    #!/usr/bin/env sh
    set -e
    branch=$(git rev-parse --abbrev-ref HEAD)
    changed=$(git diff --cached --name-only)

    if [ "$branch" = "main" ]; then
        echo "[ precommit ] On main — full test suite is required to pass."
        just format
        just check
        just test
        exit 0
    fi

    echo "[ precommit ] On '$branch' — format/lint enforced; run tests at your disgression."
    if echo "$changed" | grep -q '^gallery-backend/'; then
        just backend-format
        just backend-check
    fi
    if echo "$changed" | grep -q '^gallery-frontend/'; then
        just frontend-format
        just frontend-check
    fi
